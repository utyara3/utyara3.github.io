---
layout: content-layout.njk
title: Способности
description: Как правильно активировать их с помощью скрипта
author: utyara3
order: 3
---

## Активные способности

В игре активные способности играют важную роль. Они очень полезны, в независимости от предмета, и часто новички не знают как их правильно активировать.

Просто активировать предметы, которые не имеют анимации (квотерстафф, к примеру), не составляет труда:
```
?item.GetCooldown("quarterstaff") < 1
  equip quarterstaff
  activate r
```

Но вот с предметами, анимации которых занимают больше 1 кадра все немного сложнее.

Например, мы хотим активировать бердыш через камень разума. Если мы попытаемся сделать что то похожее, то мы активируем способность буквально на мгновение и не нанесем урона.

Дело в том, что в тот момент как мы активировали бердыш, он сразу уходит на перезарядку. Т.е. `item.GetCooldown("bardiche")` выдает значение, которое больше 0, а соответственно это наша проверка на время перезарядки возвращает число, большее 1 и код выполняется дальше, тем самым реэкипируя наш бердыш в дальнейшем.

Тогда, можно придумать что-то типа такого:
```
?item.GetCooldown("bardiche")=0 |
^item.GetCooldown("bardiche")>870
  equip bardiche
  activate r
```

Этот вариант имеет право на существование, но все же есть проблема, а именно точные числа перезарядки предметов.

Первая проблема - их надо вычислить. Придется выводить на экран перезарядку предмета в кадрах, подбирать нужное значение.

Вторая проблема - если мы зачаруем оружие на скорость атаки и/или используем зелье берсерка, то мы будем бездействовать некоторое время, т.к. для бердыша значение 870 уже будет мало, он сработает намного быстрее и мы попусту простоим с бердышем в руках после его активации.

Есть более универсальный способ:
```
?item.CanActivate() & 
^(item.GetCooldown("bardiche") <= 0 
^| item.right = "bardiche"
^ & item.right.state = 2)
  equip bardiche
  activate r
```

Это отличный вариант для использования способностей. Переведем код в слова:
- Если мы можем активировать способности `item.CanActivate()`
- И (либо перезарядка бердыша <= 0, либо мы уже держим его в руках и он имеет `cast` состояние)

Таким образом, наше умное условие будет определять, можно ли активировать способность, а также не реэкипировать оружие до момента пока оно не закончит свой каст.

Но писать такое большое условие чтобы каждый раз активировать какую то способность очень громоздко, поэтому можно сделать функцию, и активировать уже ее:

```
func CanUse(item)
  ?item.CanActivate() & 
  ^(item.GetCooldown(item) <= 0 
  ^| item.right = item
  ^ & item.right.state = 2)
    return true
  return false

?CanUse("bardiche")
  equip bardiche
  activate r 
```

Пример выше относится не только к бердышу, но и к остальным предметам, каст способности которых занимает время.

## Активация талисманов

С талисманами все намного легче. Призыв юнита тоже имеет анимацию, но сделать мы можем куда проще:

```
//Вариант 1
?summon.GetId() ! "voidweaver"
  equipR aether talisman
  activate r

//Или

?summon.GetId() ! "cinderwisp"
  equipR fire talisman
  activate r 

//----------------------------

//Вариант 2
?summon.count = 0
  equipR aether talisman
  activate r
```

## Активация жезлов и посохов

Активация жезлов и посохов, даже если и занимает какое то время, но происходит это так:
- Вы активируете способность, и время замирает
- После того как закончилась анимация, к времени на локации просто добавляется время каста способности.

Данная специфика активации также характерна для маски (занимает 1 кадр) и КПБ (занимает около 60 кадров)

С посохами и жезлами немного сложнее, чем, к примеру, с маской. Нужно убедиться, что у вас в инвентаре есть специальный жезл или посох со скрытой способностью. Получить их вы можете, если вы перекинете зачарование на вашем **10*** предмете.

Также при активации стоит убедиться что у вас достаточно брони для активации способности.

Рассмотрим активацию на примере инфернального посоха:
```
?item.GetCooldown("staff_fire") < 1 & 
^armor*10 + armor.f > 60
  equip staff_hidden_fire
  activate r 
```

Что хочется отметить по коду:
- Нужно знать правильный айди способности (`staff_руна` или `wand_руна`)
- Нужно знать как правильно экипировать предмет со скрытой способностью (обычно хватает просто `staff_hidden_руна` или `wand_hidden_руна`)
- Интересно реализована проверка брони

Отдельно скажу как работает проверка на броню в этом примере.

## Броня

В игре броня имеет как целую, так и дробную часть. Но если вы в коде используете просто `armor`, то вам вернется только целая часть (например, если ваша броня 6.7, то вы получите 6).

Получить дробную часть брони можно при помощи `armor.f`. Но стоит отметить, что дробная часть возвращается в виде целого числа (например, если ваша броня 6.7, то вы получите 7)

Таким образом, если мы умножим нашу целую часть на 10 и прибавим к ней дробную, то мы получим то же самое что и получили бы если бы умножили дробное число на 10.

Затем мы также умножаем количество брони, которое нужно для активации способности на 10.

Рассмотрим конкретнее:

Активация инфернального посоха требует 6 брони. Если у меня сейчас 6.0 брони, то, теоретически, я могу активировать посох, но если я бежал с щитом, и уберу его из рук, то моя броня начнет уменьшаться, и может стать 5.9, что не будет хватать для активации.

Поэтому, в коде мы делаем проверку `> 60`. Если у меня будет 6.1 брони, то наше условие будет такое: `6*10+1>60` что верно, и мы можем активировать посох

Названия и айди всех способностей в игре (на момент начала 2026 года):

|  Название          | Айди в стоунскрипте |
|-------------------|--------------------|
|Эфирный талисман 	 | "aether_talisman"|
|Бердыш        	     |"bardiche"|
|Таранный щит       |"bash"|
|Клинок Павшего Бога | "blade"|
|Золинка 	          |"cinderwisp"|
|Маска культиста 	|"mask"|
|Стремительный щит 	|"dash"|
|Огенный талисман 	|"fire_talisman"|
|Топор 	|"hatchet"|
|Тяжелый молот 	|"heavy_hammer"|
|Камень разума 	|"mind"|
|Квотерстафф 	|"quarterstaff"|
|Рука скелета 	|"skeleton_arm"|
|Ткач пустоты 	|"voidweaver"|
|Жезл бедствий 	|"wand_aether"|
|Взрывной жезл 	|"wand_fire"|
|Морозный жезл 	|"wand_ice"|
|Чумной жезл 	|"wand_poison"|
|Сбрасывающий жезл 	|"wand_vigor"|
|Жезл гравитации 	|"wand_stone"|
|Акробатический посох |"staff_stone"|
|Посох берсерка |"staff_poison"|
|Посох предотвращения |"staff_vigor"|
|Хватательный посох |"staff_aether"|
|Инфернальный посох|"staff_fire"|
|Посох вечности|"staff_ice"|
|Леденечный жезл|"lollipop_wand"|

